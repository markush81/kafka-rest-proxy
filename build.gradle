/*
 *  Copyright 2016, 2018 Markus Helbig
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.1.7.RELEASE'
    }
}


plugins {
    id 'java'
    id 'idea'
    id 'com.github.lkishalmi.gatling' version '3.0.1'
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

repositories {
    mavenCentral()
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_12
    targetCompatibility = JavaVersion.VERSION_12
}

test {
    exclude '**/*IntegrationTest'
}

task integrationTest(type: Test) {
    include '**/*IntegrationTest'
}
test.finalizedBy integrationTest

jar {
    manifest {
        attributes("Implementation-Title": "Kafka REST Proxy",
                "Implementation-Version": version)
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

javadoc {
    options.addBooleanOption('html5', true)
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

gatling {
}
build.dependsOn gatlingClasses


artifacts {
    archives sourcesJar
    archives javadocJar
}


dependencies {
    compile 'org.scala-lang:scala-library:2.11.+'

    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.kafka:spring-kafka'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile('org.springframework.kafka:spring-kafka-test') {
        exclude module: 'log4j'
    }
}

gatling {
    toolVersion = '3.0.0'
}

idea {
    targetVersion = '19'
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
